
new;

//Define vectors to plot
stock_return = { -0.093,
                 -0.1185,
                 -0.2197,
                 0.2836,
                 0.1074,
                 0.0483,
                 0.1561,
                 0.0548,
                 -0.3655,
                 0.2594,
                 0.1482,
                 0.0210,
                 0.1589,
                 0.3215,
                -0.1510,
                -0.0241,
                 0.1108,
                 0.0974,
                 0.0672,
                 0.2100 };
bond_return = { 0.1666,
                0.0557,
                0.1512,
                0.0038,
                0.0449,
                0.0287,
                0.0196,
                0.1021,
                0.2010,
               -0.1112,
                0.0846,
                0.1604,
                0.0297,
               -0.0910,
                0.0137,
                0.0254,
                0.0134,
                0.0358,
                0.0280,
                0.0400 };

//Portfolio starting value in thousands of dollars
starting_value = 10;

//Percentage of portfolio in bonds and stocks
bond_pct = 0.4;
stock_pct = 1 - bond_pct;

//Pre-initialize portfolio value matrix
num_years = rows(bond_return);
portfolio_value = zeros(num_years + 1, 2);

//Fill in starting value of stocks and bonds
portfolio_value[1, 1] = starting_value .* bond_pct;
portfolio_value[1, 2] = starting_value .* stock_pct;

//Calculate portfolio value from year 1 to year 'num_years'
stock_adjustment = 1 + stock_return;
bond_adjustment = 1 + bond_return;

for i(2, num_years + 1, 1);
    portfolio_value[i, 1] = portfolio_value[i-1, 1] .* bond_adjustment[i-1];
    portfolio_value[i, 2] = portfolio_value[i-1, 2] .* stock_adjustment[i-1];
endfor;

//Plot portfolio value using cumulative area plot
struct plotControl myPlot;
myPlot = plotGetDefaults("area");

plotSetTitle(&myPlot, "Portfolio Value", "Arial", 24);

plotSetLegend(&myPlot, "Bonds"$|"Stocks", "top left inside");
plotSetYLabel(&myPlot, "Thousands of US $", "Arial", 16);
plotSetXLabel(&myPlot, "");

//Set fill type to solid and opacity to 60%
plotSetFill(&myPlot, 1, 0.6);

years = seqa(2000, 1, num_years + 1);
plotArea(myPlot, years, portfolio_value);
// -*- mode: C++;-*-
